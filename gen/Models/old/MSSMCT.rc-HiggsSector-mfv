(* ------------------------------- tan(beta) ------------------------------- *)

ABRules = {CA -> 1, SA -> 0, CA2 -> 1, SA2 -> 0, C2A -> 1, S2A -> 0,
           CAB -> CB, SAB -> SB, CBA -> CB, SBA -> SB,
           SBA2 -> SB^2, CBA2 -> CB^2}

RenConst[dZH1] := -UVDivergentPart[Re[DSelfEnergy[S[2] -> S[2], 0]]] //. ABRules

RenConst[dZH2] := -UVDivergentPart[Re[DSelfEnergy[S[1] -> S[1], 0]]] //. ABRules


(* ---------------------------- Higgs sector ------------------------------- *)
(* The dMHiggs1 follow hep-ph/0611326.                                       *)
(* TB -> TB (1 + dTB1) (= Eq. 50 in hep-ph/0611326) instead of               *)
(* TB -> TB + dTB1 makes it necessary to replace dTB1 -> dTB1/TB:            *)

RenConst[dTh01] := TadpoleRC[S[1]]

RenConst[dTHH1] := TadpoleRC[S[2]]

RenConst[dTA01] := TadpoleRC[S[3]]


dMHconst = EL/(2 MZ SW CW)

If[ TrueQ[$MHpInput],
RenConst[dMHiggs1[5, 5]] := ReTilde[SelfEnergy[S[5] -> S[5], MHptree]];
RenConst[dMHiggs1[3, 3]] := dMHiggs1[5, 5] - dMWsq1;
RenConst[dMHiggs1[3, 4]] :=
  dMHconst (dTHH1 SBA - dTh01 CBA) - dTB1 CB^2 (MHptree^2 - MW^2),
(* else *)
RenConst[dMHiggs1[3, 3]] := ReTilde[SelfEnergy[S[3] -> S[3], MA0tree]];
RenConst[dMHiggs1[5, 5]] := dMHiggs1[3, 3] + dMWsq1;
RenConst[dMHiggs1[3, 4]] :=
  dMHconst (dTHH1 SBA - dTh01 CBA) - dTB1 CB^2 MA0tree^2
]

RenConst[dMHiggs1[1, 1]] := (dMHiggs1[3, 3] CBA^2 + dMZsq1 SAB^2 + 
   dMHconst (dTHH1 CBA SBA^2 - dTh01 SBA (1 + CBA^2)) +
   2 dTB1 CB^2 (MZ^2 SAB CAB - MA0tree^2 SBA CBA))

RenConst[dMHiggs1[2, 2]] := (dMHiggs1[3, 3] SBA^2 + dMZsq1 CAB^2 -
   dMHconst (dTHH1 CBA (1 + SBA^2) - dTh01 SBA CBA^2) -
   2 dTB1 CB^2 (MZ^2 SAB CAB - MA0tree^2 SBA CBA))

RenConst[dMHiggs1[4, 4]] := -dMHconst (dTHH1 CBA + dTh01 SBA)

RenConst[dMHiggs1[1, 2]] := -dMHiggs1[3, 3] SBA CBA - dMZsq1 SAB CAB -
   dMHconst (dTHH1 SBA^3 + dTh01 CBA^3) -
   dTB1 CB^2 (MA0tree^2 (CBA^2 - SBA^2) + MZ^2 (CAB^2 - SAB^2))

RenConst[dMHiggs1[1, 3]] := -dMHconst SBA dTA01

RenConst[dMHiggs1[1, 4]] := +dMHconst CBA dTA01

RenConst[dMHiggs1[2, 3]] := -dMHconst CBA dTA01

RenConst[dMHiggs1[2, 4]] := -dMHconst SBA dTA01

RenConst[dMHiggs1[6, 6]] := dMHiggs1[4, 4]

(* To be consistent with the FA/FC convention: Sigma_ij <--> j -> i,         *)
(* we changed the sign of I dTA01:                                           *)
RenConst[dMHiggs1[5, 6]] :=
  dMHconst (dTHH1 SBA - dTh01 CBA + I dTA01) - dTB1 CB^2 MHptree^2

RenConst[dMHiggs1[6, 5]] := Conjugate[dMHiggs1[5, 6]]


RenConst[dZHiggs1[1, 1]] := SA^2 dZH1 + CA^2 dZH2;
RenConst[dZHiggs1[2, 2]] := CA^2 dZH1 + SA^2 dZH2;
RenConst[dZHiggs1[3, 3]] := SB^2 dZH1 + CB^2 dZH2;
RenConst[dZHiggs1[4, 4]] := CB^2 dZH1 + SB^2 dZH2;
RenConst[dZHiggs1[1, 2]] := SA CA (dZH2 - dZH1);
RenConst[dZHiggs1[1, 3]] := 0;
RenConst[dZHiggs1[1, 4]] := 0;
RenConst[dZHiggs1[2, 3]] := 0;
RenConst[dZHiggs1[2, 4]] := 0;
RenConst[dZHiggs1[3, 4]] := SB CB (dZH2 - dZH1);

(* RenConst[dZHiggs1[5, 5]] := dZHiggs1[3, 3] leads to IR divergent results. *)
(* For proper on-shell conditions: add dZ^hat, see arXiv:1111.7289.          *)
(* This cancels dZHiggs1[3, 3], leading to an IR finite result.              *)
RenConst[dZHiggs1[5, 5]] := -ReDiag[DSelfEnergy[S[5] -> S[5], MHptree]]

RenConst[dZbarHiggs1[5, 5]] := dZHiggs1[5, 5]

RenConst[dZHiggs1[6, 6]] := dZHiggs1[4, 4]

RenConst[dZHiggs1[5, 6]] := dZHiggs1[3, 4]

RenConst[dZHiggs1[6, 5]] := dZHiggs1[3, 4]

