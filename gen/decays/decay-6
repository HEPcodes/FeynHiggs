#! /bin/sh

test $# -ne 1 && {
  echo 'Usage:  decay tag'
  echo 'generates the "tag" decay'
  echo '- will output diagrams if DEBUG=1'
  echo '- turn off simplification with NOSIMP=1'
  exit 1
}

tag=`basename $1`

shopt -s nullglob

: ${DEBUG=0}
echo DEBUG=$DEBUG
: ${NOSIMP=0}
echo NOSIMP=$NOSIMP

mkdir -p f m
rm -fr f/$tag-squaredme f/$tag-renconst m/$tag.*
test $DEBUG = 1 && rm -f d/$tag-*

math -run tag=\"$tag\" -run nosimp=$NOSIMP -run debug=$DEBUG << \_EOF_ > m/$tag.log
<< FeynArts`;

<< FormCalc`;

(*SetOptions[CalcFeynAmp, RetainFile -> True];*)
(*ReadFormDebug[1, "decay.log"];*)

FormPre = Identity;
FormSub = Identity;

(*loopopt[1] = LastSelections -> F[15];*)
_loopopt = Sequence[];

simp = If[nosimp === 0, Identity, Simplify];
fsimp = If[nosimp === 0, Identity, FullSimplify];

NClear[];

N[sqrt2] = Sqrt[2.];
sqrt2/: sqrt2/2 = 1/sqrt2;
sqrt2/: sqrt2^2 = 2;
sqrt2/: sqrt2^-2 = 1/2;

Sq[m1] = m12;
Sq[m2] = m22;
Sq[m3] = m32;
Sq[MHiggs[a_]] = MHiggs2[a];
MH = MHiggs[hno];
MH2 = MHiggs2[hno];
Mh0tree = Mh0;
Mh0tree2 = Mh02;
MHHtree = MHH;
MHHtree2 = MHH2;
MA0tree = MA0;
MA0tree2 = MA02;

Sq[GS] =.;
GS = GSMH;
Sq[GSMH] = 4 Pi AlfasMH;
Sq[GSDb] = 4 Pi AlfasDb;

Sq[EL] =.;
EL = EL0;
Sq[EL0] = 4 Pi Alfa0;

trigrulz = {
  CA a_ + CA2 b_ -> CA (a + CA b),
  SA a_ + SA2 b_ -> SA (a + SA b),
  CB a_ + CB2 b_ -> CB (a + CB b),
  SB a_ + SB2 b_ -> SB (a + SB b)
};

frulz[_ -> 0] = {};

frulz[t_ -> ty_] := {
  Mf[t,1] -> Mf[ty,1],
  Mf[t,2] -> Mf[ty,2],
  Mf[t,3] -> Mf[ty,3],
  Mf[t,g_] -> Mf[ty,g],
  Mf2[t,1] -> Mf2[ty,1],
  Mf2[t,2] -> Mf2[ty,2],
  Mf2[t,3] -> Mf2[ty,3],
  Mf2[t,g_] -> Mf2[ty,g] };

sfrulz[t_ -> 0] := {		(* for SM *)
  Mqy[t,g_] -> Mf[t,g] };

sfrulz[t_ -> ty_] := {
  Mqy[t,g_] -> Mf[ty,g],
  MSf[s_,t,g_] -> MSf[s,ty,g],
  MSf2[s_,t,g_] -> MSf2[s,ty,g],
  USf[s__,t,g_] -> USf[s,ty,g],
  USfC[s__,t,g_] -> USfC[s,ty,g],
  MASf[as_,t] -> MASf[as,ty],
  MASf2[as_,t] -> MASf2[as,ty],
  UASf[as__,t] -> UASf[as,ty],
  UASfC[as__,t] -> UASfC[as,ty],
  Kf[g__,t] -> Kf[g,ty],
  KfC[g__,t] -> KfC[g,ty] };

massrulz[u_, uy_, d_, dy_] := Flatten[{
  frulz[3 -> u],
  sfrulz[3 -> uy],
  frulz[4 -> d],
  sfrulz[4 -> dy] }];

final[expr_] := expr /.
  gather[q_, a_:1] :> sgather[reim[q] a] /.
  sgather -> split /.
  { Finite -> 1,
    USf[a__] USfC[a__] -> USf2[a],
    ZHiggs[i_, j_] -> XHiggs[i, j, uzext],
    IndexDelta -> IDelta,
    Sqrt[2] -> sqrt2,
    1/Sqrt[2] -> 1/sqrt2,
    re[h_[x__]] :> ToSymbol[h, "Re"][x],
    im[h_[x__]] :> ToSymbol[h, "Im"][x] } /.
  IDelta -> Delta;

(*Mdy[g_] := Mfy[7, g];*)
Sq[Mfy[a__]] = Mfy2[a];
RealQ[_Mfy] = True;

Dim[gno1] = Dim[gno2] = 3;

Format[Mino3, FortranForm] := "M_3";
Format[Mino3C, FortranForm] := "M_3C";


$DebugCmd = {"#ifdef DETAILED_DEBUG\n", "#endif\n", "DCOUP ", " ENDL"};

deepestOnly[{___, l_}] = {l};

SetOptions[Paint,
  PaintLevel :> deepestOnly[InsertionLevel],
  ColumnsXRows -> {3, 4}];

paint[ins_, suffix_] := (
  MkDir["d"];
  $PaintSE = ToFileName["d", tag <> "-"];
  Paint[ ins,
    DisplayFunction -> (Export[$PaintSE <> suffix <> ".ps",
      #, "PS", ImageSize -> 144 {3, 4} ]&) ]
) /; debug === 1;


SetOptions[WriteExpr, FinalCollect -> True];


Attributes[wfok] = {Orderless};
wfok[] = True;
wfok[_, S[4]] = wfok[_, V[2]] = True;
_wfok = False;


_amprulz = {};

amps[loop_, proc_, model_, excl_, foo_, opt___] :=
Block[ {io, top, ins, insv, insct, amp, vert},
  SetOptions[InsertFields, Model -> model, opt];

  io = Length[Flatten[{#}]]&/@ proc;

  top = CreateTopologies[loop, io,
    ExcludeTopologies -> {Tadpoles, WFCorrections[excl]}];
  ins = InsertFields[top, proc, loopopt[loop]];
  insv = DiagramSelect[ins, wfok@@ WFCorrectionFields[##] &];
  paint[insv, "v"];

  top = CreateCTTopologies[loop, io,
    ExcludeTopologies -> {TadpoleCTs, WFCorrectionCTs[excl]}];
  ins = InsertFields[top, proc];
  insct = DiagramSelect[ins, wfok@@ WFCorrectionCTFields[##] &];
  paint[insct, "ct"];

  amp = CreateFeynAmp[Join[insv, Head[insv]@@ insct]];

  SetOptions[CalcFeynAmp, Dimension -> If[model === SM, D, 4]];

  amp = OffShell[amp, 1 -> m1,
    2 -> ((If[# === 0, m2 = m22 = 0]; m2)&),
    3 -> ((If[# === 0, m3 = m32 = 0]; m3)&)];

FormSub = simp1 (*Identity*) (*TermCollect*) (*Simplify*);
  vert = CalcFeynAmp[foo[amp /. amprulz[loop]]];
  vert = final[vert];
(*
  vert = Collect[vert, {_Mat, _SumOver, _Den, _IndexDelta}, simp1];
*)

  Abbreviate[vert, 5, Preprocess -> simp1 (*, Deny -> HffDb*)] //elimDiv
];


IDelta/: IDelta[i_, j_] a_ := IDelta[i, j] (a /. j -> i) /; !FreeQ[a, j];


fxi[x_] := fsimp[x //. trigrulz] /. trigrulz;

Attributes[fsi] = {Listable};
fsi[x_] := x /; FreeQ[x, Plus];
fsi[x_] := fsi[x] = fxi[fxi[x]];

xi[x_] := simp[x //. trigrulz] /. trigrulz;

Attributes[si] = {Listable};
si[x_] := x /; FreeQ[x, Plus];
si[x_] := si[x] = xi[xi[x]];

simp1 = OnSize[100, fsi, 250, si, Map];
simp2 = fsimp;

loopint = _A0 | _B0 | _B1 | _B00 | _B11 | _DB0 | _DB1 | _DB00 | _DB11;

SetOptions[PrepareExpr,
  Expensive -> List@@ loopint,
  FinalTouch -> simp2];


elimDiv[expr_] := ToIndexIf[ expr /.
  (d:Den[MHiggs2[i_], MHiggs2[j_]]) -> d IndexDiff[i, j] ];


SetOptions[WriteSquaredME,
  FilePrefix -> tag <> "-",
  SymbolPrefix -> tag <> "_",
  Folder -> tag <> "-squaredme",
  FileIncludes -> {"#include \"externals.h\"\n\
#include \"types.h\"\n\
#include \"debug.h\"\n\n",
    "#include \"inline.h\"\n"},
  FileHeader -> StringReplace[FileHeader /. Options[WriteSquaredME],
    "#endif" -> "* this file is part of FeynHiggs\n#endif"]];

SetOptions[WriteRenConst,
  Folder -> tag <> "-renconst"];

job[proc_, model_, excl_, foo_, opt___] :=
Block[ {v0, v1, col, abbr, sub, uzext},
  v0 = amps[0, proc, model, excl, foo, opt];
  v1 = amps[1, proc, model, excl, foo, opt];

  uzext = If[ MatchQ[v0, _[0]], uzext0, uzext1 ];

  col = ColourME[All, All];
  abbr = OptimizeAbbr[final[Abbr[]]];
  sub = OptimizeAbbr[Subexpr[]] //fsi;

  Put[{v0, v1, col, abbr, sub}, ToFileName["m", tag <> ".amp"]];

  rcs = FindRenConst[{v0, v1, sub}];

  SetOptions[WriteSquaredME, SubroutineIncludes -> {
    If[ Length[rcs] === 0,
      "#include \"Decay.h\"\n",
      "#include \"Decay.h\"\n#include \"" <> tag <> "-renconst.h\"\n" ],
    "#include \"inline.h\"\n" }];

  WriteSquaredME[v0, v1, col, abbr, sub, "f",
    ExtraRules -> ToOldBRules,
    SymbolPrefix -> tag <> "_",
    LoopSquare -> True];	(* <<<!!! *)

  If[ Length[rcs] > 0,
FormSub = simp1;
    rcs = final[foo[CalcRenConst[rcs, loopopt[1]] /. amprulz[1]] /.
      ToOldBRules];
    Put[rcs, ToFileName["m", tag <> ".rc"]];
    WriteRenConst[rcs, "f"] ];
];


gather/: gather[q_, expr_:1] x_ := gather[q, expr x] /;
  FreeQ[x, gather];

gather/: gather[q_, a_:1] + gather[q_, b_:1] := gather[q, a + b] /;
  FreeQ[{a, b}, gather];


sgather/: sgather[x_] + sgather[y_] := sgather[x + y];

sgather/: sgather[x_] y_ := sgather[x y];

sgather[0] = 0;


split[x_] := ReTilde[x /. reim -> re] + cI ImTilde[x /. reim -> im];


$CKM = True;
$FV = {12, 13, 14};
cMSSMhgaga = {"MSSMQCD", "dAlfa", "KW", "FV", "Kf", "HGpGm", "Yuk", "HMixExt"};
cMSSM = DeleteCases[cMSSMhgaga, "HGpGm"];
SM = {"SMQCD", "dAlfa", "Yuk", "dZH1"};

Hel[3] = Hel[2]


decay["hgaga"] := job[
  S[10, {hno}] -> {V[1], V[1]},
  cMSSMhgaga, _,
  # /. massrulz[tH, tH, 0, bBR] /. {
    Yuk[3|4, _, g_, g_] :> hgagaQ,
    Yuk[13|14, _, g_, g_] :> hgagaSq,
    Hbb[h_, g_, g_] :> HffDb[0, h, 4, g] Mf[4, g]/Mf[bBR, g] } &
];

decay["hgagaSM"] := job[
  S[1] -> {V[1], V[1]},
  SM, _,
  # /. massrulz[tH, tH, 0, 0] /. {
    Yuk[3|4, _, g_, g_] :> hgagaQ } &
];


decay["hgaZ"] := job[
  S[10, {hno}] -> {V[1], V[2]},
  cMSSMhgaga, _,
  # /. massrulz[tH, tH, 0, bBR] /. {
    _Yuk -> 1,
    Hbb[h_, g_, g_] :> HffDb[0, h, 4, g] Mf[4, g]/Mf[bBR, g] } &
];

decay["hgaZSM"] := job[
  S[1] -> {V[1], V[2]},
  SM, _,
  # /. massrulz[tH, tH, 0, 0] /. _Yuk -> 1 &
];


decay["hgg"] := job[
  S[10, {hno}] -> {V[5], V[5]},
  cMSSM, _,
  simp[# /. massrulz[tH, tH, 0, bBR] /. {
    Yuk[3, h_, g_, g_] :> hggU[h, g],
    Yuk[4, h_, g_, g_] :> gather[hggD[h, g]],
    Yuk[_, h_, g_, g_] :> hggSq[h],
    Hbb[h_, g_, g_] :> HffDb[0, h, 4, g] Mf[4, g]/Mf[bBR, g] }]&
];

decay["hggSM"] := job[
  S[1] -> {V[5], V[5]},
  SM, _,
  simp[# /. massrulz[tH, tH, 0, 0] /. {
    Yuk[3, h_, g_, g_] :> hggU[h, g],
    Yuk[4, h_, g_, g_] :> gather[hggD[h, g]] }]&
];


decay["hhh"] :=
Block[ {amprulz},
  amprulz[1] = DeltaHhh -> 0;
  _amprulz = {};
  job[ S[10, {hno}] -> {S[10, {hno1}], S[10, {hno2}]},
    cMSSM /. "FV" -> "Hhh", 2|3,
    # /. massrulz[3, 3, bTR, bTR] /. {
      _Yuk -> 1,
      _Hbb -> 1 } &,
    ExcludeParticles -> {U[5], V[5]} ]
];


hffjob[args__] :=
Block[ {dMWsq1, EL0 = ELMZ, Alfa0 = AlfaMZ, amprulz},
  Options[dMWsq1] = {ExcludeParticles -> {}};	(* i.e. V[1] allowed here *)
  SetOptions[Abbreviate, Deny -> HffDb];
  amprulz[0] = Hbb[h_, g_, g_] :> HffDb[sub1L, h, 4, g];
  amprulz[1] = _Hbb -> 1;
  job[ args,
    # /. massrulz[tH, tH, bH, bH] /. _Yuk -> 1 &,
    ExcludeParticles -> {V[1], V[5]} ]
];

Hff[t_] := hffjob[
  S[10, {hno}] -> {F[t, {gno1}], -F[t, {gno2}]},
  cMSSM, 2|3
];

HffSM[t_] := hffjob[
  S[1] -> {F[t, {gno1}], -F[t, {gno2}]},
  SM, _
];

decay["hee"] := Hff[2];

decay["heeSM"] := HffSM[2];

decay["huu"] := Hff[3];

decay["huuSM"] := HffSM[3];

decay["hdd"] := Hff[4];

decay["hddSM"] := HffSM[4];


(*
decay["hcc"] := write[ CalcFeynAmp @
  amps[1, S[10, {hno}] -> {-F[12,{c1}], F[12,{c2}]}, "HMix"] ];
decay["hnn"] := write[ CalcFeynAmp @
  amps[1, S[10, {hno}] -> {F[11,{n1}], F[11,{c2}]}, "HMix"] ];
*)


_decay := Abort[];


decay[tag];

Uninstall[$ReadFormHandle];
_EOF_


# prepare generated code for inclusion in FH

for file in d/*.ps ; do gzip -f $file ; done

cd f

test -d $tag && rm -fr $tag
mkdir $tag

convert() {
  script='
/INI_S/d
/INI_ANGLE/d
/DEINI/d
/seq(.) = seq(.) + 1/d
/PAR_PREP/d
/PAR_SYNC/d
s/PAR_EXEC(\([^,]*\), \([^)]*\))/call \1(\2)/
s/SIMD_CEIL(\([^)]*\))/\1/
/SIMD/d
/#define LEGS/d
/#if PARALLEL/,/#endif/d
/BEGIN HSEL/,/END HSEL/d
s/ResType/RealType/g
s/HelType/ComplexType/g
s/HelInd(\([^)]*\))/\1/g
s/HelDim(\([^)]*\))/\1/g
s/HelSum(\(.*\))/\1/
s/, helicities//
s/, flags//
/flags/d
/hbits/d
/helicities/d
/MASK_HEL/d
/call hdd_v0/i\
\	sub1L = 1\n\
\	call hdd_v0\n\
\	Cloop(1) = Ctree(1)\n\
\	Ctree(1) = 0\n\
\n\
\	sub1L = 0
/hdd-BEGIN FF_LOOP/,/hdd-END FF_LOOP/ {
/Cloop(1) = 0/d
}
'

  for file in $1/*.[Fh] ; do
    f=`basename "$file"`
    sed "$script" "$file" | sed '
:1
/^$/{N
s/^\n$//
t 1
}' > $tag/$f
  done
  rm -fr $1
}

rm -f $tag-squaredme/$tag-specs.h

convert $tag-squaredme
convert $tag-renconst

